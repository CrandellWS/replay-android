apply plugin: 'com.android.library'

repositories {
    flatDir{
        dirs 'libs'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"
    defaultConfig {
        applicationId "io.replay.framework"
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"

        //commenting out tests because of the various changes we've made (Parth - Aug 22, 2014)
        testApplicationId "io.replay.framework.tests"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        def STRING = "String"
        def REPLAY_URL = "REPLAY_URL"
        emulator.initWith(buildTypes.debug)
        emulator {
            debuggable true
            buildConfigField STRING, REPLAY_URL, "\"10.0.2.2:3000/\""
            //stupidly, without the extra strings, it tries to make "10.0.2.2" into a very very weird Long.

        }
        genymotion.initWith(buildTypes.debug)
        genymotion {
            debuggable true
            buildConfigField STRING, REPLAY_URL, "\"192.168.52.1:3000/\""
        }
        release {
            debuggable false
            buildConfigField STRING, REPLAY_URL, "\"api.replay.io/\""
        }
    }
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.aar'])
    //compile(name: 'jobqueue', ext: 'aar')
    compile project(':jobqueue')
}

task clearJar(type: Delete) {
    delete 'build/libs/replay-android.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', 'replay-android.jar')
}

makeJar.dependsOn(clearJar, build)
